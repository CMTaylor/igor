"Igor Query Language" {

IgorQuery
	= [ [ RelationshipPattern Combinator ] '$' ] RelationshipPattern
	;

RelationshipPattern
	= { SubjectPattern Combinator } SubjectPattern
	;

SubjectPattern
	= InstancePattern
	| BranchPattern
	;

BranchPattern
	= '(' RelationshipPattern ')'
	;

InstancePattern
	= ( ClassPattern | IdentifierPattern | PositionPattern | PredicatePattern )
	  { IdentifierPattern | PositionPattern | PredicatePattern }
	;

ClassPattern
	= name
	| name '*'
	| '*'
	;

IdentifierPattern
	= '#' Identifier
	;

PositionPattern
	= ':' position
	;

PredicatePattern
	= '[' predicate ']'
	;

Identifier
	= integer
	| string
	| word
	;

Combinator
	= whitespace
	| '>'
	| '~'
	;
}
